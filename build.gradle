import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'eclipse'
	id 'java'
}

def latestJava = 17
def oldestJava = 8

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(latestJava))
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-source', '' + oldestJava, '-target', '' + oldestJava]
}

compileTestJava.options.encoding = 'UTF-8'
compileJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()

	// Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// Paper and Adventure API
	maven {
		url 'https://repo.papermc.io/repository/maven-public/'
	}

	// Simple Voice Chat
	maven {
		name = "henkelmax.public"
		url = 'https://maven.maxhenkel.de/repository/public'
	}

	// Skript
	maven {
		url 'https://repo.skriptlang.org/releases'
	}

}

dependencies {

	// Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.700')

	// Spigot/Bukkit
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.20.2-R0.1-SNAPSHOT')

	// Simple Voice Chat
	implementation (group: 'de.maxhenkel.voicechat', name: 'voicechat-api', version: '2.5.0')

	// Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.3') {
		transitive = false
	}

}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ("lang/") {
		include '*.lang'
		into 'lang/'
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveVersion = project.property("version")
	minimize()
}
